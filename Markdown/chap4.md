# 第四章 Excel函数-逻辑判断

> Excel中有着非常丰富的函数，这些函数能大幅度提升我们的工作效率，本节中会重点介绍一些常用的Excel函数。

## 1.if函数

打开文件`data/chap4/4.1.xlsx`，点击【案例1】，现在需要对客户的原始得分进行校正，当客户的原始得分为负时，校正为0

针对上面遇到的问题，可以使用IF函数，Excel函数用法如下

【IF函数】**=IF(判断条件，如果满足则，如果不满足则)**

针对遇到的问题，那么在C2单元格中输入

**=IF(B2<0,0,B2)**            ----> **注意，这里的逗号要使用英文的逗号**

那么这个公式是什么意思呢？判断B2单元格是不是小于0，如果是，则C2单元格为0，如果不是则返回B2单元格的值，可以看到C2单元格实际返回值为0，这是因为B2的值为-1，满足判断条件，因此C2单元格返回为0

那么如何进行公式批量填充呢？

这里推荐一个比较优雅的方法

1.在C2单元格写Excel公式

2.选择需要填充公式的区域，包括C2单元格

3.点击C2单元格，进入公式编辑状态

4.使用快捷键`CTRL`+`Enter`进行批量填充

![4.1](.\src\chap4\4.1.gif)

打开文件`data/chap4/4.1.xlsx`，点击【案例2】，现在有客户的1月、2月消费金额，如果客户2个月的消费金额都超过400，那么就认为这个客户为高消费客户。

这里与5.1中的案例不同，需要IF函数与逻辑函数进行配合

【AND函数】**=AND(条件1，条件2，...)** 

【OR函数】**=OR(条件，条件2，...)**

AND函数，需要所有条件全部满足，才返还TRUE，OR函数，所有条件中只要有1个条件满足，就返还TRUE

针对遇到的问题，那么在D2单元格中输入

**=IF(AND(B2>400,C2>400),"高消费客户","")**  -----> 这里""代表返回空值

随后对整列的函数进行填充，具体填充方法见5.1中详解

![4.2](.\src\chap4\4.2.gif)

## 2.ifs函数

打开文件`data/chap4/4.1.xlsx`，点击【案例3】，有员工的相关考核分，考核得分在4.8分以上，优秀；考核得分在3.5-4.7分，良好；考核得分在0-3.4分，一般。

这里可以使用IFS函数，具体用法为

【IFS函数】=IFS(判断条件1,如果满足条件1则,判断条件2,如果满足条件2则)

针对遇到的问题，则可以在C2单元格输入

**=IFS(B3<=3.4,"一般",AND(B3>=3.5,B3<=4.7),"良好",B3>=4.8,"优秀")**

对整列数据进行函数填充

![4.3](.\src\chap4\4.3.gif)

## 3.sumif函数

>  在Excel数据任务处理中，会遇到大量的求和的任务，SUMIF函数在求和任务中具有天然优势，因此在本节中将详细的讲解SUMIF函数的相关用法

【SUMIF函数】=SUMIF(条件区域，求和条件，[实际求和区域])  --->实际求和区域位选择条件

### 案例1

打开`data/chap4/4.2xlsx`中的【案例1】现在有几名同学的考试成绩，现在想统计数学成绩在90分以上的成绩之和

那么可以输入如下的函数公式

**=SUMIF(B:B,">=90",B:B)**

第一个参数为条件区域，因为要判断数学成绩，所以这里选择B列，当然这里也可以修改为实际的区域B2:B12

第二个参数为判定条件，判定条件为大于等于90

第三个参数为实际求和区，这是选填参数

![4.4](.\src\chap4\4.4.gif)

### 案例2

打开`data/chap4/4.2xlsx`中的【案例2】，现在想统计小米系列的产品销售额

SUMIF函数支持模糊匹配，因此我们输入如下公式

**=SUMIF(B:B,"小米*",C:C)**

使用小米*匹配所有的小米系产品

![4.5](.\src\chap4\4.5.gif)

### 案例3

打开`data/chap4/4.2xlsx`中的【案例3】，现在想统计1月-3月的预计销售额预计销售和实际销售额

因此可以巧用SUMIF函数，将条件区域进行横向选择，条件选择自己需要的字段，因此输入如下两个函数公式，并向下拖拽填充其他位置

`=SUMIF($B$2:$G$2,I$2,B3:G3)`

`=SUMIF($B$2:$G$2,H$2,B3:G3)`

`$`在Excel中是绝对引用，公式中引用的单元格如果使用绝对引用 `$`，那么向下拖拽的过程中，这个单元格不会增加。

![4.6](.\src\chap4\4.6.png)

例如，在J3单元格中输入`=C3`，在K3单元格中输入`=$C$3`并向下拖拽进行公式填充，观察区别

发现，如果使用绝对引用，引用格式值并不会随着公式的拖拽而改变，这就是绝对引用与非绝对引用的区别。

![4.7](.\src\chap4\4.7.gif)

### 案例4

打开`data/chap4/4.2xlsx`中的【案例4】，现在需要统计语文成绩在80分以上，90分以下同学的数学成绩之和

那么遇到这种情况应该怎么处理呢？相比于前面的案例，这里有2个筛选条件，而SUMIF函数无法满足需求，这里可以使用SUMIFS函数

【SUMIFS函数】=SUMIFS（求和区域，条件区域1，条件1，条件区域2，条件2）

因此写如下函数公式

**=SUMIFS(C2:C13,B2:B13,">80",B2:B13,"<90")**

因为要求数学成绩，所以第一个参数为C2:C13，而筛选条件为语文，因此条件区域为B2:B13

![4.8](.\src\chap4\4.8.gif)

## 4.countifs函数

打开`data/chap4/4.2xlsx`中的【案例5】，现在需要统计语文成绩超过60，数学成绩超过65，英语成绩超过70的同学个数

那么遇到这种情况应该怎么处理呢？其实countifs函数与sumifs函数用法非常接近，求满足所有条件的个数

【COUNTIFS函数】=COUNTIFS（条件区域1，条件1，条件区域2，条件2）

因此写如下函数公式

**=COUNTIFS(B2:B13,">60",C2:C13,">65",D2:D13,">70")**

![4.9](.\src\chap4\4.9.gif)

## 练习

1.选择`data/chap4/4.1.xlsx`工作簿中的【案例1】，将客户原始得分为1以下的校正为0

2.选择`data/chap4/4.1.xlsx`工作簿中的【案例2】，客户1月2月中只要有1个月消费超过400，即为"高消费客户"

3.选择`data/chap4/4.1.xlsx`工作簿中的【案例3】，能否不使用IFS函数，使用IF函数通过嵌套完成相应的务。

4.打开`data/chap4/4.2xlsx`中的【案例1】语文成绩在80分以上的同学成绩之和

5.打开`data/chap4/4.2xlsx`中的【案例2】统计所有苹果系列产品的销售额

6.打开`data/chap4/4.2xlsx`中的【案例6】完成所有手机的销售额求和
